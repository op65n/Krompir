plugins {
    id "com.github.johnrengelman.shadow" version "7.0.0"
    id "java"
}

group = "tech.op65n.krompir"
version = "1.0.5-Beta"

repositories {
    flatDir {
        dirs "libs"
    }
}

repositories {
    mavenCentral()
    maven { url "https://repo.papermc.io/repository/maven-public/" }
    maven { url "https://oss.sonatype.org/content/groups/public/" }
    maven { url "https://repo.extendedclip.com/content/repositories/placeholderapi/" }
    maven { url "https://repo.mattstudios.me/artifactory/public" }
    maven { url "https://jitpack.io" }
}

dependencies {
    implementation name: "FrozenActions.1.0.1"

    implementation "me.mattstudios.utils:matt-framework-gui:2.0.3.3"
    implementation "me.mattstudios.utils:matt-framework:1.4"

    compileOnly "me.clip:placeholderapi:2.10.9"
    compileOnly "com.github.TechFortress:GriefPrevention:16.17.0"
    compileOnly "io.papermc.paper:paper-api:1.19.2-R0.1-SNAPSHOT"
}

shadowJar {
    archiveFileName.set("Krompir.jar")
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}